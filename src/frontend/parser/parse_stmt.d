module frontend.parser.parse_stmt;

import frontend.parser;
import frontend.lexer;
import frontend;

import std.path : extension;
import std.stdio;

class ParseStmt
{
private:
    Parser p;

public:
    this(Parser p)
    {
        this.p = p;
    }

    Node[] parseBody()
    {
        Node[] body;
        if (p.match(TokenKind.LBrace))
        {
            while (!p.check(TokenKind.RBrace))
                body ~= p.parseIntern();
            p.consume(TokenKind.RBrace, "Expected '}'.");
        }
        else
            body ~= p.parseIntern();
        return
        body;
    }

    Node parseIfStmt(Position pos, bool isElse = false)
    {
        IfStmt _else = null;
        Node expr = isElse ? null : p.parseExpr.parse();

        // cobre casos de else if
        if (p.match(TokenKind.If))
            expr = p.parseExpr.parse();

        Node[] body = parseBody();

        if (!p.isAtEnd() && p.check(TokenKind.Else))
            _else = cast(IfStmt) parseIfStmt(p.advance().pos, true);

        return new IfStmt(expr, body, _else, isElse, pos);
    }

    Node parseReturnStmt(Position pos)
    {
        Node val = p.check(TokenKind.SemiColon) ? null : p.parseExpr.parse();
        return new ReturnStmt(val, pos);
    }

    Node parseDeferStmt(Position pos)
    {
        return new DeferStmt(p.parseExpr.parse(), pos);
    }

    Node parseWhileStmt(Position pos)
    {
        Node expr = p.parseExpr.parse();
        Node[] body = parseBody();
        return new WhileStmt(expr, body, pos);
    }

    Node parseForStmt(Position pos)
    {
        Node first, middle, end;
        p.consume(TokenKind.LParen, "Expected '('.");

        if (!p.check(TokenKind.SemiColon))
        {
            TypeExpr t = p.parseType.parse();
            Token name = p.consume(TokenKind.Id, "Expected 'ID'.");
            first = p.parseDecl.parseVarDecl(t, name);
        }

        p.consume(TokenKind.SemiColon, "Expected ';'.");
        if (!p.check(TokenKind.SemiColon))
            middle = p.parseExpr.parse();

        p.consume(TokenKind.SemiColon, "Expected ';'.");
        if (!p.check(TokenKind.RParen))
            end = p.parseExpr.parse();

        p.consume(TokenKind.RParen, "Expected ')'.");

        Node[] body = parseBody();
        return new ForStmt(first, middle, end, body, pos);
    }

    Node parseLabelStmt(Token name)
    {
        Node[] body;
        while (!p.isAtEnd() && (!p.check(TokenKind.RBrace)))
            body ~= p.parseIntern();
        // writeln(body);
        return new LabelStmt(name.s, body, name.pos);
    }

    Node parseGotoStmt(Position pos)
    {
        Node name = p.parseExpr.parse();
        return new GotoStmt(name, pos);
    }

    Node parseImportStmt(Position pos)
    {
        Node dir = p.parseExpr.parse();
        if (dir.kind != NodeKind.IdentExpr && dir.kind != NodeKind.MemberExpr && dir.kind != NodeKind
            .StringLit)
        {
            p.err.error(dir.pos, "The previous directory is invalid.");
            return dir;
        }
        string d = resolveDir(dir);
        if (d.length == 0)
            p.err.error(dir.pos, "The import directory cannot be null.");
        
        if (extension(d) != ".cx" && extension(d) != "")
            p.err.error(dir.pos, "The imported file is not a valid '.cx' file.");

        ImportStmt stmt = new ImportStmt(d, p.getPos(pos, dir.pos));
        Program prog = new Program([stmt]);
        new ImportResolver(p.ctx, prog, p.err, p.types, p.generic).resolve();
        p.imports ~= prog.body;
        return stmt;
    }

    string resolveDir(Node node)
    {
        if (node.kind == NodeKind.IdentExpr)
            return (cast(IdentExpr) node).val;
        if (node.kind == NodeKind.StringLit)
            return (cast(StringLit) node).val;
        if (node.kind == NodeKind.MemberExpr)
        {
            MemberExpr m = cast(MemberExpr) node;
            return resolveDir(m.left) ~ "/" ~ resolveDir(m.right);
        }
        p.err.error(node.pos, "Invalid value passed for directory resolution.");
        return "";
    }

    Node parseSwitchStmt(Position pos)
    {
        Node expr = p.parseExpr.parse();
        p.consume(TokenKind.LBrace, "Expected '{'.");
        CaseStmt[] cases;
        while (!p.isAtEnd() && !p.check(TokenKind.RBrace))
        {
            Node node = p.parseStmt.parse();
            if (node.kind != NodeKind.CaseStmt)
            {
                p.err.error(node.pos, "Invalid case on switch statement.");
                continue;
            }
            cases ~= cast(CaseStmt) node;
        }
        p.consume(TokenKind.RBrace, "Expected '}'.");
        return new SwitchStmt(expr, cases, pos);
    }

    Node parseCaseStmt(Position pos, bool isDefault)
    {
        Node value = isDefault ? null : p.parseExpr.parse();
        p.consume(TokenKind.Colon, "Expected ':' after case statement.");
        Node[] body;
        bool close, hasVar;
        while (!p.isAtEnd() && !close && !p.check(TokenKind.Case) && !p.check(TokenKind.Default))
        {
            if (p.check(TokenKind.RBrace))
                break;
            Node node = p.parseIntern()[0];
            if (node.kind == NodeKind.ContinueOrBreakStmt || node.kind == NodeKind.ReturnStmt)
                close = true;
            if (node.kind == NodeKind.VarDecl)
                hasVar = true;
            body ~= node;
        }
        return new CaseStmt(value, hasVar, body, pos);
    }

	Node parseForEachStmt(Position pos)
    {
        // foreach is not supported in Cx. C has no iterator protocol
        // and Cx does not add one. The half-implemented version only
        // worked on structs with an `iter` method, not on arrays --
        // a misfit. Use an index loop for arrays
        // (`for (int i = 0; i < n; i++)`), or a pointer loop for
        // linked structures.
        p.err.error(pos,
            "'foreach' is not supported in Cx: C has no iterator " ~
            "protocol. Use an index loop (`for (int i = 0; i < n; i++)`) " ~
            "for arrays, or a pointer loop for linked structures.");

		// Consume the whole statement so parsing continues cleanly.
        // Source shape: foreach [k,] v ; value { body }
        //
        // The `;` in the middle is a syntax separator, not a
        // statement terminator. Skip to and past it, then skip the
        // iterable expression, then consume the body block.
        while (!p.isAtEnd() && !p.check(TokenKind.SemiColon))
            p.advance();
        if (p.check(TokenKind.SemiColon))
            p.advance();

        while (!p.isAtEnd()
               && !p.check(TokenKind.LBrace)
               && !p.check(TokenKind.SemiColon))
            p.advance();

        if (p.check(TokenKind.LBrace))
        {
            int depth = 0;
            while (!p.isAtEnd())
            {
                if (p.check(TokenKind.LBrace)) { depth++; p.advance(); }
                else if (p.check(TokenKind.RBrace))
                {
                    depth--;
                    p.advance();
                    if (depth == 0) break;
                }
                else p.advance();
            }
        }
        else if (p.check(TokenKind.SemiColon))
            p.advance();

        return new RawStmt("/* foreach not supported */", pos);
    }

    Node parse()
    {
        Token tk = p.advance();
        switch (tk.kind)
        {
        case TokenKind.Defer:
            return parseDeferStmt(tk.pos);

        case TokenKind.Return:
            return parseReturnStmt(tk.pos);

        case TokenKind.If:
            return parseIfStmt(tk.pos);

        case TokenKind.While:
            return parseWhileStmt(tk.pos);

        case TokenKind.For:
            return parseForStmt(tk.pos);

        case TokenKind.Break:
        case TokenKind.Continue:
            return new ContinueOrBreakStmt(tk.kind == TokenKind.Break, tk.pos);

        case TokenKind.Goto:
            return parseGotoStmt(tk.pos);

        case TokenKind.Import:
            return parseImportStmt(tk.pos);

        case TokenKind.Raw:
            return new RawStmt(tk.s, tk.pos);

        case TokenKind.Switch:
            return parseSwitchStmt(tk.pos);

        case TokenKind.Case:
        case TokenKind.Default:
            return parseCaseStmt(tk.pos, tk.kind == TokenKind.Default);

        case TokenKind.ForEach:
            return parseForEachStmt(tk.pos);

        default:
            return new IdentExpr("null", new TypeExprNamed("void", tk.pos), tk.pos);
        }
    }
}
